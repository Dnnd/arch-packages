# Maintainer: Danila Maslennikov <d.v.maslennikov@outlook.com>
pkgname=v2ray-plugin-fresh-git
pkgrel=1
pkgver=r117.80f59c3
pkgdesc="v2ray shadowsocks plugin with fresh v2ray-core version"
arch=('x86_64')
url="https://github.com/Dnnd/v2ray-plugin"
license=('MIT')
makedepends=('git' 'go') # 'bzr', 'git', 'mercurial' or 'subversion'
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('v2ray-plugin-fresh-git::git+https://github.com/Dnnd/v2ray-plugin.git')
sha256sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/${pkgname}"
    go build -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -o "${pkgname%-git}" 
}


package() {
    cd "$srcdir/${pkgname}"
    install -d  "$pkgdir/usr/bin/"
    install -m755 "${pkgname%-git}" "$pkgdir/usr/bin/v2ray-plugin"
}
