# Maintainer: Danila Maslennikov <d.v.maslennikov@outlook.com>
pkgname=sway-menus-git
pkgver=r7.7da075e
pkgrel=1
pkgdesc="Menus collection for sway"
arch=('x86_64')
url="https://github.com/Dnnd/sway-menus"
license=('Apache')
depends=('sway' 'systemd')
optdepends=('wofi' 'rofi')
makedepends=('git' 'go') 
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
source=('sway-menus-git::git+https://github.com/Dnnd/sway-menus.git')
md5sums=('SKIP')

pkgver() {
    cd "$srcdir/${pkgname}"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
    cd "$srcdir/${pkgname}"
    go build -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -o "${pkgname%-git}" 
}


package() {
    cd "$srcdir/${pkgname}"
    install -d  "$pkgdir/usr/bin/"
    install -m755 "${pkgname%-git}" "$pkgdir/usr/bin/sw-menus"
}
